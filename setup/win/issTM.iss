; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!
 #define MyAppName "EmulatorTM"  
#define MyAppExeName "EmulatorTM.exe"
[Setup]
AppVersion=1.0.0
VersionInfoVersion=1.0.0
VersionInfoTextVersion=1.0.0

AppName=ЭмуляторТМ
AppVerName=«ЭмуляторТМ»
DefaultGroupName=ЭмуляторТМ

AppPublisher=ПАО «Электромеханика», СКБ 103
VersionInfoCompany=ПАО «Электромеханика»

AppPublisherURL=http://www.elmeh.ru
AppSupportURL=http://www.elmeh.ru
AppUpdatesURL=http://www.elmeh.ru

DefaultDirName={pf32}\EmulatorTM\
OutputDir=.\
OutputBaseFilename=SETUP
Compression=lzma
SolidCompression=true
DisableDirPage=true
DisableReadyMemo=true
DisableReadyPage=true
UsePreviousAppDir=false
DisableProgramGroupPage=true
UsePreviousGroup=false
RestartIfNeededByRun=false
ShowLanguageDialog=no
ChangesAssociations=yes

SetupIconFile=setup\IconFile.ico

[Languages]
Name: "russian"; MessagesFile: "compiler:Languages\Russian.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Dirs]
Name: {app}; Permissions: everyone-modify
Name: {app}\bin; Permissions: everyone-modify

[Files]
Source: "setup\EmulatorTM.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "setup\bin\*"; DestDir: "{app}\bin"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "setup\libusb-1.0 (dll)\*"; DestDir: "{app}\libusb-1.0 (dll)"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "setup\libusb-1.0.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "setup\libusb-1.0.exp"; DestDir: "{app}"; Flags: ignoreversion
Source: "setup\libusb-1.0.iobj"; DestDir: "{app}"; Flags: ignoreversion
Source: "setup\libusb-1.0.ipdb"; DestDir: "{app}"; Flags: ignoreversion
Source: "setup\libusb-1.0.lib"; DestDir: "{app}"; Flags: ignoreversion
Source: "setup\libusb-1.0.pdb"; DestDir: "{app}"; Flags: ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Registry]
Root: HKCU; Subkey: "Software\Classes\.myp\OpenWithProgids"; ValueType: string; ValueName: "EmulatorTMFile.myp"; ValueData: ""; Flags: uninsdeletevalue
Root: HKCU; Subkey: "Software\Classes\EmulatorTMFile.myp"; ValueType: string; ValueName: ""; ValueData: "EmulatorTM File"; Flags: uninsdeletekey
Root: HKCU; Subkey: "Software\Classes\EmulatorTMFile.myp\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\EmulatorTM.exe,0"
Root: HKCU; Subkey: "Software\Classes\EmulatorTMFile.myp\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\EmulatorTM.exe"" ""%1"""
Root: HKCU; Subkey: "Software\Classes\Applications\EmulatorTM.exe\SupportedTypes"; ValueType: string; ValueName: ".myp"; ValueData: ""
Root: HKLM; Subkey: "SOFTWARE\Microsoft\Windows NT\CurrentVersion\AppCompatFlags\Layers";ValueType: String; ValueName: "{app}\EmulatorTM.exe"; ValueData: "RUNASADMIN";  Flags: uninsdeletekeyifempty uninsdeletevalue; MinVersion: 0,6.1

[Icons]
Name: "{group}\EmulatorTM"; Filename: "{app}\EmulatorTM.exe"
Name: "{userdesktop}\EmulatorTM"; Filename: "{app}\EmulatorTM.exe"; Tasks: desktopicon;  \
  AfterInstall: SetElevationBit('{userdesktop}\EmulatorTM.lnk')
  
[Code]

procedure SetElevationBit(Filename: string);
var
  Buffer: string;
  Stream: TStream;
begin
  Filename := ExpandConstant(Filename);
  Log('Setting elevation bit for ' + Filename);

  Stream := TFileStream.Create(FileName, fmOpenReadWrite);
  try
    Stream.Seek(21, soFromBeginning);
    SetLength(Buffer, 1);
    Stream.ReadBuffer(Buffer, 1);
    Buffer[1] := Chr(Ord(Buffer[1]) or $20);
    Stream.Seek(-1, soFromCurrent);
    Stream.WriteBuffer(Buffer, 1);
  finally
    Stream.Free;
  end;
end;

[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: runascurrentuser nowait postinstall skipifsilent

